#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.
# $Id: configure.in,v 1.8 2009-08-17 07:00:42 jacky Exp $
#
# Date:   2008-04-21
#
# Copyright (C) 2007-2008  TERD TENCENT
#
# ================================================================
# boilerplate and flags

AC_PREREQ(2.59)

AC_COPYRIGHT([[Copyright 2007-2010 IERD TENCENT. All rights reserved.]])
AC_REVISION([$tsf4g: configure.in,v 1.0  $])


AC_INIT(tsf4g,,,[Tencent Software Framework for Game])
AC_CONFIG_SRCDIR([lib_src/comm/shtable.c])

AC_CONFIG_AUX_DIR(build)

eval `$ac_aux_dir/version.sh`
if test -z "$TSF4G_STRING"; then
	AC_MSG_ERROR([could not determine version])
fi

if test -f "$ac_aux_dir/shtool" && test ! -d $ac_aux_dir/shtool; then
	ac_cv_shtool="$ac_aux_dir/shtool"
else
	AC_MSG_ERROR([no shtool found in $ac_aux_dir])
fi

SHTOOL="$ac_cv_shtool"
dnl AC_SUBST(SHTOOL)dnl

TB="" TN=""
if test -t 1; then
	TB="`$SHTOOL echo -e '%B' 2>/dev/null`"
	TN="`$SHTOOL echo -e '%b' 2>/dev/null`"
fi


echo "Configuring ${TB}${TSF4G_STRING}${TN} ..."


AM_INIT_AUTOMAKE([$TSF4G_PACKAGE],[$TSF4G_VERSION], [no defines])dnl
#==================================================================
# Restricted form of AC_ARG_ENABLE that limits user options
#
# $1 = option name
# $2 = help-string
# $3 = default value	(auto).  "--" means do not set it by default
# $4 = allowed values (auto yes no)
# $5 = overridden default
AC_DEFUN([TSF4G_ARG_ENABLE], [# TSF4G --enable-$1
	pushdef([tsf4g_DefVal],ifelse($3,,auto,$3))
	AC_ARG_ENABLE($1,ifelse($4,,[$2],[$2] translit([$4],[ ],[|])) ifelse($3,--,,@<:@tsf4g_DefVal@:>@),[
	tsf4g_arg=invalid
	for tsf4g_val in ifelse($4,,[auto yes no],[$4]) ; do
		if test "$enableval" = "$tsf4g_val" ; then
			tsf4g_arg="$tsf4g_val"
		fi
	done
	if test "$tsf4g_arg" = "invalid" ; then
		AC_MSG_ERROR(bad value $enableval for --enable-$1)
	fi
	tsf4g_enable_$1="$tsf4g_arg"
]ifelse($3,--,,[,
[	tsf4g_enable_$1=ifelse($5,,tsf4g_DefVal,[${]$5[:-]tsf4g_DefVal[}])]]))dnl
dnl AC_MSG_RESULT([TSF4G -enable-$1 $tsf4g_enable_$1])
	popdef([tsf4g_DefVal])
# end --enable-$1
])dnl

dnl --------------------------------------------------------------------
dnl Restricted form of AC_ARG_WITH that limits user options
dnl
dnl $1 = option name
dnl $2 = help-string
dnl $3 = default value (no)
dnl $4 = allowed values (yes or no)
AC_DEFUN([TSF4G_ARG_WITH], [# TSF4G --with-$1
	AC_ARG_WITH($1,[$2 @<:@]ifelse($3,,yes,$3)@:>@,[
	tsf4g_arg=invalid
	for tsf4g_val in ifelse($4,,[yes no],[$4]) ; do
		if test "$withval" = "$tsf4g_val" ; then
			tsf4g_arg="$tsf4g_val"
		fi
	done
	if test "$tsf4g_arg" = "invalid" ; then
		AC_MSG_ERROR(bad value $withval for --with-$1)
	fi
	tsf4g_with_$1="$tsf4g_arg"
],
[	tsf4g_with_$1=ifelse($3,,"no","$3")])dnl
dnl AC_MSG_RESULT([TSF4G --with-$1 $tsf4g_with_$1])
# end --with-$1
])dnl

# ================================================================
# Start Args
AC_MSG_CHECKING(configure arguments)
AC_PREFIX_DEFAULT(/usr/local)

TSF4G_HOME=`pwd`


# ==================================================================

# ----------------------------------------------------------------
# General "enable" options
TSF4G_ARG_ENABLE(debug,[  --enable-debug 	  enable debugging], no, [no yes])dnl
TSF4G_ARG_ENABLE(link_static,[  --enable-link_static 	  enable link static ], no, [no yes])dnl
TSF4G_ARG_ENABLE(64bit,[  --enable-64bit 	  enable 64bit ], yes, [no yes])dnl


# ----------------------------------------------------------------
# --with-obus
TSF4G_ARG_WITH(obus,[  --with-obus    with old bus library in tsf4g library],
	no, [yes no] )

TSF4G_ARG_WITH(sec,[  --with-sec    with sec library in tsf4g library ],
	yes, [yes no] )

#TSF4G_ARG_WITH(dbms_tdr,[  --with-dbms-tdr    with dbms support  in tdr library ],
#	yes, [yes no] )

dnl ----------------------------------------------------------------
# Checks for programs.
AC_PROG_CC



# Checks for header files.

# Checks for typedefs, structures, and compiler characteristics.

# ================================================================
# Checks for library functions.

AC_CHECK_LIB(expat, XML_Parse, ,
	[AC_MSG_ERROR([TSF4G depends on Expat library, but unable to find it.
	  If you have  this library in a nonstandard directory, please tell me by
		environment variables<LDFLAGS,CPPFLAGS>; if you have not this library ,at $TSF4G_HOME/deps,
		you can find the Expat library version to install, also you can download
		lastest version at http://www.libexpat.org,		and then install Expat
		library according to its 'INSTALL'  document!])
	])

AC_CHECK_LIB(scew, scew_parser_tree, ,
	[AC_MSG_ERROR([TSF4G depends on scew library, but unable to find it.
	  If you have  this library in a nonstandard directory, please tell me by
		environment variables<LDFLAGS,CPPFLAGS>; if you have not this library ,at $TSF4G_HOME/deps,
		you can find the scew library version to install,	also you can download
		lastest version at http://www.nongnu.org/scew/, and then  install
		scew library according to its 'INSTALL' document!])
	])

AC_CHECK_HEADERS(mysql.h,[],[AC_MSG_ERROR([No mysql.h present,Please tell me where I can find the header
by using CPPFLAGS='-I Path to mysql.h'])])



# ================================================================


#AC_MSG_NOTICE([tsf4g_enable_debug=$tsf4g_enable_debug, CFLAGS=$CFLAGS])
if test "$tsf4g_enable_debug" = yes; then
   mode=debug
   CFLAGS="-g -pg"
else
   mode=release
   CFLAGS="-g -O2 -fno-strict-aliasing"
fi



#AC_MSG_NOTICE([tsf4g_enable_debug=$tsf4g_enable_debug, CFLAGS=$CFLAGS])
TSF4G_ARCH=`uname -m || echo unknown`

if test "$TSF4G_ARCH" = x86_64; then
  	if test "$tsf4g_enable_64bit" = yes; then
		CFLAGS+=" -fPIC -Wmissing-prototypes "
	else
		CFLAGS+=" -m32 -fPIC"
	fi
fi


if test "$tsf4g_enable_link_static" = yes; then
   LTSTATIC="-static"
else
   LTSTATIC=""
fi

#AC_MSG_NOTICE(["CFLAGS=$CFLAGS"])

AC_SUBST(CFLAGS)
AC_SUBST(TSF4G_HOME)
AC_SUBST(MYSQL_INSTALL_DIR)
AC_SUBST(LTSTATIC)
TSF4G_BIN=$TSF4G_HOME/bin
AC_SUBST(TSF4G_BIN)
AC_SUBST(tsf4g_enable_debug)
AC_SUBST(tsf4g_with_sec)
AC_SUBST(tsf4g_with_obus)
AC_SUBST(TSF4G_ARCH)

# ================================================================
AC_CONFIG_FILES([tsf4g.mk:build/tsf4g.mk.in])
AC_OUTPUT




echo
echo "*** $TSF4G_PACKAGE $TSF4G_VERSION has been successfully configured ***"
echo
echo "Type 'make' to complete the compilation ($mode mode) and you can"
echo "type 'make test' to test the result of compilation and then"
echo "'make install' as root to install."
echo
echo "Run './configure --help' to see all available options."
echo
